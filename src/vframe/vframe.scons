#-------------------------------------------------------------------------------
#
#     application build construction script
#
import os
import sys

from pathlib import Path

sys.path.append( '.scons' )

from helpers import *

#-------------------------------------------------------------------------------
#
#     General
#
ProductName = 'vframe'

Import('env')

Platform = env['PLATFORM']

CurDir = os.path.abspath(str(Dir('.').srcnode()))
env['CURDIR'] = CurDir

#-------------------------------------------------------------------------------
#
#     Setup sources and target
#
Sources    = ['vframe.cpp', 'video.cpp']
ExtSources = ['#src/utils/socket.cpp']

Target = str(env['BINDIR'] / ProductName)

#  VariantDir
src_path   = str(Dir('#src'))
build_path = str(env['BUILDPATH'])

build_vframe_path = str(env['BUILDPATH'] / ProductName)
build_utils_path  = str(env['BUILDPATH'] / 'utils')

VariantDir(build_vframe_path, '#src/vframe', duplicate=0)
VariantDir(build_utils_path,  '#src/utils',  duplicate=0)

Src    = [str(env['BUILDPATH'] / ProductName / s) for s in Sources]
ExtSrc = [s.replace('#src/utils', build_utils_path) for s in ExtSources]

#-------------------------------------------------------------------------------
#
#     Setup local construction environment
#

DEFINES = ['ENA_FW_QT']

if Platform == 'win32':
    DEFINES += ['BOOST_ALL_NO_LIB']

BOOST_PATH = Path(os.environ['EXTERNAL_LIB']) / 'boost' / env['BOOST_VERSION']
FMTPATH    = Path(os.environ['EXTERNAL_LIB']) / 'fmt'
SPDLOGPATH = Path(os.environ['EXTERNAL_LIB']) / 'spdlog'

PYTHON_VER_MAJOR = '3'
PYTHON_VER_MINOR = '10'
PYTHON_VERSION   = PYTHON_VER_MAJOR + '.' + PYTHON_VER_MINOR
PYTHON_VER_SFX   = PYTHON_VER_MAJOR + PYTHON_VER_MINOR

PYTHON_INCLUDE = '/usr/include/python' + PYTHON_VERSION
PYTHON_LIBRARY = 'python' + PYTHON_VERSION

BOOST_PYTHON_LIB = 'boost_python' + PYTHON_VER_SFX
BOOST_NUMPY_LIB  = 'boost_numpy'  + PYTHON_VER_SFX

env.Append(CCFLAGS = ['-D'+ d for d in DEFINES] )
env.Append(CPPPATH = [os.path.join(BOOST_PATH, 'include'), str(FMTPATH / 'include'), str(SPDLOGPATH / 'include')])
env.Append(CPPPATH = [PYTHON_INCLUDE])
env.Append(LIBPATH = [str(BOOST_PATH / 'lib'), str(FMTPATH / 'lib'), str(SPDLOGPATH / 'lib')])
env.Append(LIBS = [PYTHON_LIBRARY, BOOST_PYTHON_LIB, BOOST_NUMPY_LIB, 'fmt', 'spdlog'])
env.Append(CXXFLAGS = '-DSPDLOG_COMPILED_LIB')

env['SHLIBPREFIX'] = ''
if Platform == 'win32':
    env['SHLIBSUFFIX'] = '.pyd'

#-------------------------------------------------------------------------------
#
#     Build the targets
#
env.SharedLibrary( Target, Src + ExtSrc)

#-------------------------------------------------------------------------------

